plugins {
    id "org.sonarqube" version "3.4.0.2513"
}

ext {
    javaVersion = JavaVersion.VERSION_17
    springBootVersion = "3.0.0"
    springCloudVersion = "2022.0.0-RC3"
    micaAutoVersion = "3.0.0"
    micaWeiXinVersion = "2.1.2"
    knife4jVersion = "3.0.3"
    springdocVersion = "2.0.0"
    swaggerVersion = "1.6.9"
    swagger3xVersion = "2.2.7"
    druidVersion = "1.2.15"
    okhttpVersion = "4.10.0"
    jsoupVersion = "1.15.3"
    xxlJobVersion = "2.3.1"
    lombokVersion = "1.18.24"
    findbugsVersion = "3.0.2"
    logstashVersion = "7.2"
    lokiLogbackAppender = "1.3.2"
    zxingVersion = "3.5.1"
    jetCacheVersion = "2.7.1"
    jfinalVersion = "5.0.4"
}

configure(subprojects) {
    group = GROUPID
    version = VERSION
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    // ci 自动发布 SNAPSHOT 版本
    task publishSnapshot(type: GradleBuild) {
        if (VERSION.endsWith('SNAPSHOT')) {
            tasks = ['clean', 'build', 'publish']
        }
    }
}

apply from: "${rootProject.projectDir}/gradle/quality.gradle"
configure(subprojects - project(":mica-bom")) {
    apply plugin: "java-library"
    apply from: "${rootProject.projectDir}/gradle/publish-jar.gradle"
    sourceCompatibility = "$javaVersion"
    targetCompatibility = "$javaVersion"
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    repositories {
        mavenLocal()
        maven { url "https://repo.spring.io/release" }
        maven { url "https://repo.spring.io/milestone" }
        mavenCentral()
    }

    dependencies {
        // import BOM.
        implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
        implementation platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion")
        compileOnly "org.slf4j:slf4j-api"
        compileOnly "jakarta.servlet:jakarta.servlet-api"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
        annotationProcessor "org.projectlombok:lombok:$lombokVersion"
        testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
        // swagger 暂时有问题 https://github.com/springfox/springfox/issues/2763
//        annotationProcessor "org.springframework:spring-context-indexer:${springVersion}"
        // JSR-305 only used for non-required meta-annotations
        compileOnly "com.google.code.findbugs:jsr305:$findbugsVersion"
        testCompileOnly "com.google.code.findbugs:jsr305:$findbugsVersion"
        testCompileOnly "org.projectlombok:lombok:$lombokVersion"
        testCompileOnly "org.springframework:spring-test"
        testImplementation "org.slf4j:slf4j-api"
        testImplementation "org.junit.jupiter:junit-jupiter"
    }

    compileJava.options.compilerArgs.add '-parameters'
    compileTestJava.options.compilerArgs.add '-parameters'
    compileJava.dependsOn(processResources)
}

def getSonarToken() {
    return System.getenv('SONAR_TOKEN') ?: SONAR_TOKEN
}

def getRepositoryUsername() {
    return System.getenv('NEXUS_OSS_USER_NAME') ?: NEXUS_OSS_USER_NAME
}

def getRepositoryPassword() {
    return System.getenv('NEXUS_OSS_PASS_WORD') ?: NEXUS_OSS_PASS_WORD
}
